include(compiler_settings)
include(lib_utils)

set(GENERATED_HEADER_DIR ${CMAKE_CURRENT_BINARY_DIR}/generated_headers)

generate_shared_export_header(
	${TARGETNAME_LIB_FILESYSTEM_STDIO}
	${GENERATED_HEADER_DIR}/Filesystem/libexport.h
)

add_library(${TARGETNAME_LIB_FILESYSTEM_STDIO} SHARED
	${GENERATED_HEADER_DIR}/Filesystem/libexport.h
	include/Filesystem/filesystem.h
	src/dir.c
	src/filesystem_internal.h
	src/filesystem.c
	src/pak.c
	src/wad.c
	src/zip.c
)

target_include_directories(${TARGETNAME_LIB_FILESYSTEM_STDIO}
	PUBLIC
	${GENERATED_HEADER_DIR}
	include

	PRIVATE
	src
)

target_link_libraries(${TARGETNAME_LIB_FILESYSTEM_STDIO}
	PUBLIC
	${TARGETNAME_LIB_BUILDPLATFORM}
	${TARGETNAME_LIB_ENGINEPUBLICAPI}
	${TARGETNAME_LIB_ENGINEINTERNALAPI}

	PRIVATE
	${TARGETNAME_LIB_CRCLIB}
	${TARGETNAME_LIB_MINIZ}
	${TARGETNAME_LIB_CRTLIB}
	${TARGETNAME_LIB_BUILDPLATFORM}
	${TARGETNAME_LIB_PLATFORMLIB}
)

set_common_library_compiler_settings(${TARGETNAME_LIB_FILESYSTEM_STDIO})

target_compile_definitions(${TARGETNAME_LIB_FILESYSTEM_STDIO} PRIVATE
	FILESYSTEM_STDIO_PRODUCER
	"ALLOCA_H=<${ALLOCA_HEADER}>"
)

target_compile_options(${TARGETNAME_LIB_FILESYSTEM_STDIO} PRIVATE
	# MSVC
	$<$<STREQUAL:"${HOST_COMPILER}","MSVC">:/GR->

	# Not MSVC
	$<$<AND:$<NOT:$<STREQUAL:"${HOST_COMPILER}","MSVC">>,$<COMPILE_LANGUAGE:CXX>>:-fno-rtti>
	$<$<AND:$<NOT:$<STREQUAL:"${HOST_COMPILER}","MSVC">>,$<COMPILE_LANGUAGE:CXX>>:-fno-exceptions>
)

if(NF_ENABLE_TESTS)
	add_executable(${TARGETNAME_LIB_FILESYSTEM_TESTS}
		src/tests/caseinsensitive.c
	)

	target_link_libraries(${TARGETNAME_LIB_FILESYSTEM_TESTS} PRIVATE
		${TARGETNAME_LIB_FILESYSTEM_STDIO}
		${TARGETNAME_LIB_PLATFORMLIB}
	)
endif()

install(TARGETS ${TARGETNAME_LIB_FILESYSTEM_STDIO}
	ARCHIVE DESTINATION ${INSTALL_ROOT}
	LIBRARY DESTINATION ${INSTALL_ROOT}
	RUNTIME DESTINATION ${INSTALL_ROOT}
)
